{-# OPTIONS --without-K --safe #-}
open import Categories.Category.Core using (Category)

-- 'agda-categories' doesn't really have a great way about talking about _specific_
-- pullback squares, which we need for the definition of a Cartesian Monad.
-- To work around this, let's just hack together a quick module that defines a predicate
-- version of 'Pullback'
module Categories.Diagram.IsPullback {o â„“ e} (ğ’ : Category o â„“ e) where

open Category ğ’
open HomReasoning
open Equiv

open import Level
open import Categories.Diagram.Pullback ğ’
open import Categories.Morphism.Reasoning ğ’ as Square
  renaming (glue to glue-square) hiding (id-unique)

private
  variable
    A B X Y Z : Obj
    f g h hâ‚ hâ‚‚ i iâ‚ iâ‚‚ j k : A â‡’ B

record IsPullback {P : Obj} (pâ‚ : P â‡’ X) (pâ‚‚ : P â‡’ Y) (f : X â‡’ Z) (g : Y â‡’ Z) : Set (o âŠ” â„“ âŠ” e) where
  field
    commute   : f âˆ˜ pâ‚ â‰ˆ g âˆ˜ pâ‚‚
    universal : âˆ€ {hâ‚ : A â‡’ X} {hâ‚‚ : A â‡’ Y} â†’ f âˆ˜ hâ‚ â‰ˆ g âˆ˜ hâ‚‚ â†’ A â‡’ P
    unique    : âˆ€ {eq : f âˆ˜ hâ‚ â‰ˆ g âˆ˜ hâ‚‚} â†’
                  pâ‚ âˆ˜ i â‰ˆ hâ‚ â†’ pâ‚‚ âˆ˜ i â‰ˆ hâ‚‚ â†’
                  i â‰ˆ universal eq

    pâ‚âˆ˜universalâ‰ˆhâ‚  : âˆ€ {eq : f âˆ˜ hâ‚ â‰ˆ g âˆ˜ hâ‚‚} â†’
                         pâ‚ âˆ˜ universal eq â‰ˆ hâ‚
    pâ‚‚âˆ˜universalâ‰ˆhâ‚‚  : âˆ€ {eq : f âˆ˜ hâ‚ â‰ˆ g âˆ˜ hâ‚‚} â†’
                         pâ‚‚ âˆ˜ universal eq â‰ˆ hâ‚‚

  uniqueâ€² : (eq eqâ€² : f âˆ˜ hâ‚ â‰ˆ g âˆ˜ hâ‚‚) â†’ universal eq â‰ˆ universal eqâ€²
  uniqueâ€² eq eqâ€² = unique pâ‚âˆ˜universalâ‰ˆhâ‚ pâ‚‚âˆ˜universalâ‰ˆhâ‚‚

  id-unique : id â‰ˆ universal commute
  id-unique = unique identityÊ³ identityÊ³

  universal-resp-â‰ˆ : âˆ€ {eq : f âˆ˜ hâ‚ â‰ˆ g âˆ˜ hâ‚‚} {eqâ€² : f âˆ˜ iâ‚ â‰ˆ g âˆ˜ iâ‚‚} â†’ hâ‚ â‰ˆ iâ‚ â†’ hâ‚‚ â‰ˆ iâ‚‚ â†’ universal eq â‰ˆ universal eqâ€²
  universal-resp-â‰ˆ hâ‚â‰ˆiâ‚ hâ‚‚â‰ˆiâ‚‚ = unique (pâ‚âˆ˜universalâ‰ˆhâ‚ â—‹ hâ‚â‰ˆiâ‚) (pâ‚‚âˆ˜universalâ‰ˆhâ‚‚ â—‹ hâ‚‚â‰ˆiâ‚‚)

  unique-diagram : pâ‚ âˆ˜ h â‰ˆ pâ‚ âˆ˜ i â†’
                   pâ‚‚ âˆ˜ h â‰ˆ pâ‚‚ âˆ˜ i â†’
                   h â‰ˆ i
  unique-diagram {h = h} {i = i} eqâ‚ eqâ‚‚ = begin
    h            â‰ˆâŸ¨ unique eqâ‚ eqâ‚‚ âŸ©
    universal eq â‰ˆË˜âŸ¨ unique refl refl âŸ©
    i            âˆ
    where eq = extendÊ³ commute

IsPullbackâ‡’Pullback : IsPullback hâ‚ hâ‚‚ f g â†’ Pullback f g
IsPullbackâ‡’Pullback {hâ‚ = hâ‚} {hâ‚‚ = hâ‚‚} {f = f} {g = g} is-pullback = record
  { pâ‚ = hâ‚
  ; pâ‚‚ = hâ‚‚
  ; commute = is-pullback.commute
  ; universal = is-pullback.universal
  ; unique = is-pullback.unique
  ; pâ‚âˆ˜universalâ‰ˆhâ‚ = is-pullback.pâ‚âˆ˜universalâ‰ˆhâ‚
  ; pâ‚‚âˆ˜universalâ‰ˆhâ‚‚ = is-pullback.pâ‚‚âˆ˜universalâ‰ˆhâ‚‚
  }
  where
    module is-pullback = IsPullback is-pullback

Pullbackâ‡’IsPullback : (pullback : Pullback f g)  â†’ IsPullback (Pullback.pâ‚ pullback) (Pullback.pâ‚‚ pullback) f g
Pullbackâ‡’IsPullback pullback = record
  { commute = pullback.commute
  ; universal = pullback.universal
  ; unique = pullback.unique
  ; pâ‚âˆ˜universalâ‰ˆhâ‚ = pullback.pâ‚âˆ˜universalâ‰ˆhâ‚
  ; pâ‚‚âˆ˜universalâ‰ˆhâ‚‚ = pullback.pâ‚‚âˆ˜universalâ‰ˆhâ‚‚
  }
  where
    module pullback = Pullback pullback
